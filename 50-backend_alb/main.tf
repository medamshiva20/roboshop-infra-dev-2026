resource "aws_lb" "backend_alb"{
    name = "${var.project}-${var.environment}" # roboshop-dev
    internal = true
    load_balancer_type = "application"
    security_groups = [local.backend_alb_sg_id]
    subnets = local.private_subnet_ids

    # keeping it as false, just to delete using terraform while practice
    enable_deletion_protection = false

    tags = merge(
        {
            Name = "${var.project}-${var.environment}"
        },
        local.common_tags
    )
}

resource "aws_lb_listener" "http"{
    load_balancer_arn = aws_lb.backend_alb.arn
    protocol = "HTTP"
    port = 80

    default_action {
        type = "fixed-response"

        fixed_response {
            content_type = "text/html"
            message_body = "<h1>Hi, I am from Backend HTTP ALB"
            status_code = "200"
        }
    }
}

resource "aws_route53_record" "www"{
    zone_id = var.zone_id
    name = "*.backend-alb-${var.environment}.${var.domain_name}"
    type = "A"

    alias {
        name = aws_lb.backend_alb.dns_name
        zone_id = aws_lb.backend_alb.zone_id
        evaluate_target_health = true
    }
}